//Отчет в виде кросс-таблицы (обычный вывод в табличный документ) и картинка
&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере(ИспользованиеОтбора);
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере(РезультатОтбора)
	Результат.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПродажиОбороты.Контрагент КАК Контрагент,
	               |	ПродажиОбороты.Номенклатура КАК Номенклатура,
	               |	ПРЕДСТАВЛЕНИЕ(ПродажиОбороты.Контрагент),
	               |	ПРЕДСТАВЛЕНИЕ(ПродажиОбороты.Номенклатура),
	               |	ПродажиОбороты.СуммаОборот КАК Сумма
	               |ИЗ
	               |	РегистрНакопления.Продажи.Обороты(, , , &Номенклатура) КАК ПродажиОбороты
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	ОБЩИЕ,
	               |	Контрагент,
	               |	Номенклатура
	               |АВТОУПОРЯДОЧИВАНИЕ";
	
	Макет = РЕквизитФормыВЗначение("Отчет").ПолучитьМакет("Макет");          //&Номенклатура
	ОбластьШапкаНоменклатура = Макет.ПолучитьОбласть("Шапка|Номенклатура");
	ОбластьШапкаКонтрагент = Макет.ПолучитьОбласть("Шапка|Контрагент");
	ОбластьШапкаИтог = Макет.ПолучитьОбласть("Шапка|Итог");
	
	ОбластьСтрокаНоменклатура = Макет.ПолучитьОбласть("Строка|Номенклатура");
	ОбластьСтрокаКонтрагент = Макет.ПолучитьОбласть("Строка|Контрагент");
	ОбластьСтрокаИтог = Макет.ПолучитьОбласть("Строка|Итог");
	
	ОбластьПодвалНоменклатура = Макет.ПолучитьОбласть("Подвал|Номенклатура");
	ОбластьПодвалКонтрагент = Макет.ПолучитьОбласть("Подвал|Контрагент");
	ОбластьПодвалИтог = Макет.ПолучитьОбласть("Подвал|Итог");
	
	Если ИспользованиеОтбора Тогда
		  Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Номенклатура", "Номенклатура В Иерархии (&Номенклатура)");
		  Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
	  Иначе
		  Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Номенклатура", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаКлиент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Контрагент");
	Результат.Вывести(ОбластьШапкаНоменклатура);
	Пока ВыборкаКлиент.Следующий() Цикл
		ОбластьШапкаКонтрагент.Параметры.Заполнить(ВыборкаКлиент);
		Результат.Присоединить(ОбластьШапкаКонтрагент);
	КонецЦикла;	
	Результат.Присоединить(ОбластьШапкаИтог);
	
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Номенклатура");	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		ОбластьСтрокаНоменклатура.Параметры.Заполнить(ВыборкаНоменклатура);
		Результат.Вывести(ОбластьСтрокаНоменклатура);
		
		ВыборкаКонтрагент = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Контрагент", "ВСЕ");		
	    Пока ВыборкаКонтрагент.Следующий() Цикл
		   ОбластьСтрокаКонтрагент.Параметры.Заполнить(ВыборкаКонтрагент);
		   Результат.Присоединить(ОбластьСтрокаКонтрагент);		   
	    КонецЦикла;	
		ОбластьСтрокаИтог.Параметры.Заполнить(ВыборкаНоменклатура);
		Результат.Присоединить(ОбластьСтрокаИтог);
	КонецЦикла;	
	
	Результат.Вывести(ОбластьПодвалНоменклатура);
	ВыборкаКлиент.Сбросить();
	
	Пока ВыборкаКлиент.Следующий() Цикл
		ОбластьПодвалКонтрагент.Параметры.Заполнить(ВыборкаКлиент);
		Результат.Присоединить(ОбластьПодвалКонтрагент);
	КонецЦикла;
	
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);		
	
	Если ВыборкаОбщийИтог.Следующий() Тогда
		ОбластьПодвалИтог.Параметры.Заполнить(ВыборкаОбщийИтог);
		Результат.Присоединить(ОбластьПодвалИтог);
	КонецЕсли;
	
КонецПроцедуры
